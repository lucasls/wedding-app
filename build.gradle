buildscript {
    ext.kotlin_version = '1.2.41'
    ext.ktor_version = '0.9.2'
    ext.appengine_version = '1.9.63'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.5'
    }
}

group 'com.github.lucasls.wedding-app'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine-standard'
apply plugin: 'kotlin-kapt'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    // Servlet
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // App Engine
    compile "com.google.appengine:appengine-api-1.0-sdk:$appengine_version"
    compile 'com.google.cloud:google-cloud-datastore:1.31.0'
    testCompile "com.google.appengine:appengine-testing:$appengine_version"
    testCompile "com.google.appengine:appengine-api-labs:$appengine_version"
    testCompile "com.google.appengine:appengine-api-stubs:$appengine_version"
    testCompile "com.google.appengine:appengine-tools-sdk:$appengine_version"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Ktor
    compile "io.ktor:ktor-server-servlet:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-json:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"

    // Gson
    compile "com.github.salomonbrys.kotson:kotson:2.5.0"

    // Application
    compile project(':domain')

    // Logs
    compile 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'io.github.microutils:kotlin-logging:1.5.4'

    // JWT
    compile 'io.jsonwebtoken:jjwt:0.9.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.13'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    kaptTest 'com.google.dagger:dagger-compiler:2.13'

    // JUnit
    testCompile 'junit:junit:4.12'

    // Mock
    testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-alpha04'
}

test {
    useJUnit {
        excludeCategories "com.github.lucasls.weddingapp.test.helper.IntegrationTest"
    }
}

task integrationTest(type: Test) {

    group = 'verification'
    useJUnit {
        includeCategories "com.github.lucasls.weddingapp.test.helper.IntegrationTest"
    }

}

appengine {
    run {
        jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005', "-Dappengine.fullscan.seconds=5"]
        environment = [
                'GOOGLE_CLOUD_PROJECT'        : 'wedding-app-lu-e-ni',
                'DATASTORE_DATASET'           : 'wedding-app-lu-e-ni',
//                'DATASTORE_EMULATOR_HOST'     : 'localhost:8081',
//                'DATASTORE_EMULATOR_HOST_PATH': 'localhost:8081/datastore',
//                'DATASTORE_PROJECT_ID'        : 'wedding-app-lu-e-ni',

        ]
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}